defmodule Jido.Integration.V2.StorePostgres.AttemptStore do
  @moduledoc false

  @behaviour Jido.Integration.V2.ControlPlane.AttemptStore

  import Ecto.Query

  alias Jido.Integration.V2.Attempt
  alias Jido.Integration.V2.Auth.SecretGuard
  alias Jido.Integration.V2.Contracts
  alias Jido.Integration.V2.ControlPlane.Stores
  alias Jido.Integration.V2.Redaction
  alias Jido.Integration.V2.StorePostgres
  alias Jido.Integration.V2.StorePostgres.Repo
  alias Jido.Integration.V2.StorePostgres.Schemas.AttemptRecord
  alias Jido.Integration.V2.StorePostgres.Serialization

  @impl true
  def put_attempt(%Attempt{} = attempt) do
    with :ok <- SecretGuard.validate_durable(attempt) do
      put_attempt_transaction(attempt)
    end
  end

  @impl true
  def fetch_attempt(attempt_id) do
    case Repo.get(AttemptRecord, attempt_id) do
      nil -> :error
      record -> {:ok, to_contract(record)}
    end
  end

  @impl true
  def list_attempts(run_id) do
    from(attempt in AttemptRecord,
      where: attempt.run_id == ^run_id,
      order_by: [asc: attempt.attempt, asc: attempt.attempt_id]
    )
    |> Repo.all()
    |> Enum.map(&to_contract/1)
  end

  @impl true
  def list_recoverable_attempts do
    from(attempt in AttemptRecord,
      where:
        attempt.status in [:accepted, :running] and
          not is_nil(attempt.runtime_ref_id),
      order_by: [asc: attempt.inserted_at, asc: attempt.attempt_id]
    )
    |> Repo.all()
    |> Enum.map(&to_contract/1)
  end

  @impl true
  def update_attempt(attempt_id, status, output, runtime_ref_id, opts \\ []) do
    with :ok <- SecretGuard.validate_durable(output) do
      update_attempt_transaction(attempt_id, status, output, runtime_ref_id, opts)
    end
  end

  def reset! do
    StorePostgres.assert_started!()
    Repo.delete_all(AttemptRecord)
    :ok
  end

  defp put_attempt_transaction(attempt) do
    Repo.transaction(fn -> persist_attempt(attempt) end)
    |> normalize_transaction()
  end

  defp persist_attempt(attempt) do
    attempt
    |> to_record_attrs()
    |> then(&AttemptRecord.changeset(%AttemptRecord{}, &1))
    |> Repo.insert()
    |> case do
      {:ok, _record} ->
        :ok = register_attempt_payload_refs(attempt)
        :ok

      {:error, changeset} ->
        Repo.rollback(changeset)
    end
  end

  defp update_attempt_transaction(attempt_id, status, output, runtime_ref_id, opts) do
    Repo.transaction(fn ->
      record = fetch_attempt_or_rollback(attempt_id)
      next_epoch = Keyword.get(opts, :aggregator_epoch, record.aggregator_epoch)
      ensure_current_epoch(record, next_epoch)

      persist_attempt_update(record, status, output, runtime_ref_id, next_epoch, opts)
    end)
    |> normalize_transaction()
  end

  defp fetch_attempt_or_rollback(attempt_id) do
    case Repo.get(AttemptRecord, attempt_id) do
      nil -> Repo.rollback(:not_found)
      record -> record
    end
  end

  defp ensure_current_epoch(record, next_epoch) when next_epoch < record.aggregator_epoch,
    do: Repo.rollback(:stale_aggregator_epoch)

  defp ensure_current_epoch(_record, _next_epoch), do: :ok

  defp persist_attempt_update(record, status, output, runtime_ref_id, next_epoch, opts) do
    result =
      Repo.update_all(
        from(attempt in AttemptRecord,
          where:
            attempt.attempt_id == ^record.attempt_id and attempt.aggregator_epoch <= ^next_epoch
        ),
        set: [
          status: status,
          output: serialized_output(output),
          runtime_ref_id: runtime_ref_id,
          aggregator_id: Keyword.get(opts, :aggregator_id, record.aggregator_id),
          aggregator_epoch: next_epoch,
          updated_at: Contracts.now()
        ]
      )

    case result do
      {1, _} -> :ok
      _other -> Repo.rollback(:stale_aggregator_epoch)
    end
  end

  defp serialized_output(nil), do: nil
  defp serialized_output(output), do: output |> Redaction.redact() |> Serialization.dump()

  defp normalize_transaction({:ok, result}), do: result
  defp normalize_transaction({:error, reason}), do: {:error, reason}

  defp to_record_attrs(%Attempt{} = attempt) do
    %{
      attempt_id: attempt.attempt_id,
      run_id: attempt.run_id,
      attempt: attempt.attempt,
      aggregator_id: attempt.aggregator_id,
      aggregator_epoch: attempt.aggregator_epoch,
      runtime_class: attempt.runtime_class,
      status: attempt.status,
      credential_lease_id: attempt.credential_lease_id,
      target_id: attempt.target_id,
      runtime_ref_id: attempt.runtime_ref_id,
      output:
        if(is_nil(attempt.output),
          do: nil,
          else: attempt.output |> Redaction.redact() |> Serialization.dump()
        ),
      output_payload_ref:
        if(is_nil(attempt.output_payload_ref),
          do: nil,
          else: Serialization.dump(attempt.output_payload_ref)
        ),
      inserted_at: attempt.inserted_at,
      updated_at: attempt.updated_at
    }
  end

  defp to_contract(record) do
    Attempt.new!(%{
      attempt_id: record.attempt_id,
      run_id: record.run_id,
      attempt: record.attempt,
      aggregator_id: record.aggregator_id,
      aggregator_epoch: record.aggregator_epoch,
      runtime_class: record.runtime_class,
      status: record.status,
      credential_lease_id: record.credential_lease_id,
      target_id: record.target_id,
      runtime_ref_id: record.runtime_ref_id,
      output: Serialization.load_json(record.output),
      output_payload_ref: Serialization.load_json(record.output_payload_ref),
      inserted_at: record.inserted_at,
      updated_at: record.updated_at
    })
  end

  defp register_attempt_payload_refs(%Attempt{} = attempt) do
    case attempt.output_payload_ref do
      nil ->
        :ok

      payload_ref ->
        Stores.claim_check_store().register_reference(payload_ref, %{
          ledger_kind: :attempt,
          ledger_id: attempt.attempt_id,
          payload_field: :output,
          run_id: attempt.run_id,
          attempt_id: attempt.attempt_id
        })
    end
  end
end
